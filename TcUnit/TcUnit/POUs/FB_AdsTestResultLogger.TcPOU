<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_AdsTestResultLogger" Id="{d14d6247-3ce3-459f-be22-6ee3520279ed}" SpecialFunc="None">
    <Declaration><![CDATA[(*
    This function block reports the results from the tests using the built-in ADSLOGSTR functionality
    provided by the Tc2_System library. This sends the result using ADS, which is consumed by the "Error List"
    of Visual Studio (which can print Errors, Warnings and Messages).
*)
FUNCTION_BLOCK FB_AdsTestResultLogger IMPLEMENTS I_TestResultLogger
VAR
    PrintingTestSuiteResultNumber : UINT(1..GVL_Param_TcUnit.MaxNumberOfTestSuites);
    PrintingTestSuiteTrigger : Tc2_Standard.R_TRIG;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="LogEndResult" Id="{dba8c729-8b99-4be3-97cd-3a4fea4cd051}">
      <Declaration><![CDATA[METHOD LogEndResult
VAR_INPUT
    (* The total number of test suites *)
    NumberOfTestSuites : UINT;
    (* The total number of test cases (for all test function blocks) *)
    NumberOfTestCases : UINT;
    (* The total number of test cases that had all ASSERTS successful *)
    NumberOfSuccessfulTestCases : UINT;
    (* The total number of test cases that had at least one ASSERT failed *)
    NumberOfFailedTestCases : UINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := '%s',
                                    StrArg := '| ==========TESTS FINISHED RUNNING==========');
GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := '| Test suites: %s',
                                    StrArg := UINT_TO_STRING(NumberOfTestSuites));
GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := '| Tests: %s',
                                    StrArg := UINT_TO_STRING(NumberOfTestCases));
GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := '| Successful tests: %s',
                                    StrArg := UINT_TO_STRING(NumberOfSuccessfulTestCases));
GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := '| Failed tests: %s',
                                    StrArg := UINT_TO_STRING(NumberOfFailedTestCases));
GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := '%s',
                                    StrArg := '| ======================================');]]></ST>
      </Implementation>
    </Method>
    <Method Name="LogTestSuiteResults" Id="{3263938a-5e4f-4e5f-bfa0-9c9469439d97}">
      <Declaration><![CDATA[METHOD PUBLIC LogTestSuiteResults
VAR
    TestSuiteName : Tc2_System.T_MaxString;
    TestName : Tc2_System.T_MaxString;
    StringToPrint : Tc2_System.T_MaxString;
    
    TestsInTestSuiteCounter : UINT(1..GVL_Param_TcUnit.MaxNumberOfTestsForEachTestSuite);
    
    TestToBePrinted : FB_Test;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF PrintingTestSuiteResultNumber <= GVL_TcUnit.NumberOfInitializedTestSuites THEN
    PrintingTestSuiteTrigger(CLK := GVL_TcUnit.TestSuiteAddresses[PrintingTestSuiteResultNumber]^.AreAllTestsFinished());
    IF PrintingTestSuiteTrigger.Q THEN
        (* Remove everything except the program name + "." + test suite name *)
        TestSuiteName := F_GetTestSuiteNameFromTestInstancePath(TestInstancePath := GVL_TcUnit.TestSuiteAddresses[PrintingTestSuiteResultNumber]^.GetInstancePath());
        StringToPrint := Tc2_Standard.CONCAT(STR1 := '| Test suite $'%s$' with ID=',
                                             STR2 := UINT_TO_STRING(PrintingTestSuiteResultNumber - 1));
        StringToPrint := Tc2_Standard.CONCAT(STR1 := StringToPrint,
                                             STR2 := ' finished running');

        (* Print test suite name and ID *)
        GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_HINT,
                                            MsgFmtStr := StringToPrint,
                                            StrArg := TestSuiteName);

        (* Print number of tests in test suite *)
        StringToPrint := Tc2_Standard.CONCAT(STR1 := '| ID=',
                                             STR2 := UINT_TO_STRING(PrintingTestSuiteResultNumber - 1));
        StringToPrint := Tc2_Standard.CONCAT(STR1 := StringToPrint,
                                             STR2 := ' number of tests=');
        StringToPrint := Tc2_Standard.CONCAT(STR1 := StringToPrint,
                                             STR2 := UINT_TO_STRING(GVL_TcUnit.TestSuiteAddresses[PrintingTestSuiteResultNumber]^.GetNumberOfTests()));
        GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_HINT,
                                            MsgFmtStr := StringToPrint,
                                            StrArg := '');

        (* Print number of failed tests in test suite *)
        StringToPrint := Tc2_Standard.CONCAT(STR1 := '| ID=',
                                             STR2 := UINT_TO_STRING(PrintingTestSuiteResultNumber - 1));
        StringToPrint := Tc2_Standard.CONCAT(STR1 := StringToPrint,
                                             STR2 := ' number of failed tests=');
        StringToPrint := Tc2_Standard.CONCAT(STR1 := StringToPrint,
                                             STR2 := UINT_TO_STRING(GVL_TcUnit.TestSuiteAddresses[PrintingTestSuiteResultNumber]^.GetNumberOfFailedTests()));
        GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_HINT,
                                            MsgFmtStr := StringToPrint,
                                            StrArg := '');

        (* Iterate all print all tests in test suite *)
        FOR TestsInTestSuiteCounter := 1 TO GVL_TcUnit.TestSuiteAddresses[PrintingTestSuiteResultNumber]^.GetNumberOfTests() BY 1 DO
            TestToBePrinted := GVL_TcUnit.TestSuiteAddresses[PrintingTestSuiteResultNumber]^.GetTestByPosition(TestsInTestSuiteCounter);
            GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_HINT,
                                                MsgFmtStr := '| Test name=%s',
                                                StrArg := TestToBePrinted.GetName());
		END_FOR

        PrintingTestSuiteResultNumber := PrintingTestSuiteResultNumber + 1;
        PrintingTestSuiteTrigger(CLK := FALSE); // Reset trigger
    END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_AdsTestResultLogger">
      <LineId Id="18" Count="0" />
    </LineIds>
    <LineIds Name="FB_AdsTestResultLogger.LogEndResult">
      <LineId Id="155" Count="2" />
      <LineId Id="159" Count="1" />
      <LineId Id="158" Count="0" />
      <LineId Id="161" Count="1" />
      <LineId Id="154" Count="0" />
      <LineId Id="164" Count="4" />
      <LineId Id="163" Count="0" />
      <LineId Id="170" Count="1" />
      <LineId Id="169" Count="0" />
    </LineIds>
    <LineIds Name="FB_AdsTestResultLogger.LogTestSuiteResults">
      <LineId Id="25" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="56" Count="2" />
      <LineId Id="61" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="9" Count="1" />
      <LineId Id="8" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="66" Count="1" />
      <LineId Id="85" Count="1" />
      <LineId Id="68" Count="1" />
      <LineId Id="77" Count="1" />
      <LineId Id="76" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="73" Count="1" />
      <LineId Id="87" Count="0" />
      <LineId Id="84" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="82" Count="1" />
      <LineId Id="81" Count="0" />
      <LineId Id="89" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="101" Count="0" />
      <LineId Id="98" Count="1" />
      <LineId Id="97" Count="0" />
      <LineId Id="100" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="26" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>