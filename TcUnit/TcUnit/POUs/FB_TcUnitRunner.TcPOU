<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="FB_TcUnitRunner" Id="{857e16d6-a26c-468a-935e-aa7317c263b9}" SpecialFunc="None">
    <Declaration><![CDATA[(*
DESCRIPTION: This function block is responsible for holding track of the tests
             and executing them.
*)
FUNCTION_BLOCK FB_TcUnitRunner IMPLEMENTS I_TcUnitRunner
VAR
    (* There can be one or more tests (testnames) for every test function
       block instance *)
    RunnableTestFunctionBlockResults : ARRAY[1..SizeOfStaticArray] OF E_TestFunctionBlockRunState;

    (* Inidication of whether all test FBs have reported that they are finished *)
    AllTestFunctionBlocksFinished : BOOL := FALSE;
    
    (* Prints the results to ADS so that Visual Studio can display the results.
       This test result formatter can be replaced with something else than ADS *)
    fbADSTestResultFormatter : FB_ADSTestResultFormatter;
    TestResultPrinter : I_TestResultFormatter := fbADSTestResultFormatter;

    (* These variables (statistics) are occupied once all tests are complete *)
    AmountOfTestFunctionBlocksFinished : UINT := 0;
    AmountOfTestCases : UINT := 0;
    AmountOfFailedTestCases : UINT := 0;
    AmountOfSuccessfulTestCases : UINT := 0;

    (* Indication of that the printing of test results is complete *)
    DonePrintingTestResults : BOOL;
END_VAR
VAR CONSTANT
    SizeOfStaticArray : UINT := 300;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="RunFunctionBlocksTests" Id="{d333e6c4-a1da-4b5b-91b3-4b4808e6c299}">
      <Declaration><![CDATA[METHOD RunFunctionBlocksTests
VAR
    Counter : UINT := 0;
    BusyPrinting : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT AllTestFunctionBlocksFinished THEN
    IF GVL.AmountOfInitializedTestFunctionBlocks = 0 THEN
        AllTestFunctionBlocksFinished := TRUE;
    ELSIF GVL.AmountOfInitializedTestFunctionBlocks > 0 THEN
        FOR Counter := 1 TO GVL.AmountOfInitializedTestFunctionBlocks BY 1 DO
            IF RunnableTestFunctionBlockResults[Counter] = E_TestFunctionBlockRunState.FINISHED THEN
                AmountOfTestFunctionBlocksFinished := AmountOfTestFunctionBlocksFinished + 1;
                AmountOfTestCases := GVL.TestFunctionBlockAddressess[Counter].GetAmountOfTests() + AmountOfTestCases;
                AmountOfFailedTestCases := GVL.TestFunctionBlockAddressess[Counter].GetAmountOfFailedTests() + AmountOfFailedTestCases;
                AmountOfSuccessfulTestCases := GVL.TestFunctionBlockAddressess[Counter].GetAmountOfSuccessfulTests() + AmountOfSuccessfulTestCases;
            ELSE
                GVL.CurrentTestFunctionBlockBeingCalled := GVL.TestFunctionBlockAddressess[Counter];
                RunnableTestFunctionBlockResults[Counter] := GVL.CurrentTestFunctionBlockBeingCalled.GetRunnable().RunTests();
            END_IF
        END_FOR
        IF AmountOfTestFunctionBlocksFinished = GVL.AmountOfInitializedTestFunctionBlocks THEN
            AllTestFunctionBlocksFinished := TRUE;
        END_IF
    END_IF
END_IF

IF AllTestFunctionBlocksFinished THEN
    IF NOT DonePrintingTestResults THEN
        TestResultPrinter.Format(AmountOfTestFunctionBlocks := AmountOfTestFunctionBlocksFinished,
                                 AmountOfTestCases := AmountOfTestCases,
                                 AmountOfSuccessfulTestCases := AmountOfSuccessfulTestCases,
                                 AmountOfFailedTestCases := AmountOfFailedTestCases,
                                 Busy => BusyPrinting);
    DonePrintingTestResults := NOT BusyPrinting;
    END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_TcUnitRunner">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcUnitRunner.RunFunctionBlocksTests">
      <LineId Id="131" Count="0" />
      <LineId Id="128" Count="1" />
      <LineId Id="10" Count="1" />
      <LineId Id="69" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="83" Count="0" />
      <LineId Id="86" Count="1" />
      <LineId Id="21" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="23" Count="2" />
      <LineId Id="12" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="106" Count="1" />
      <LineId Id="143" Count="0" />
      <LineId Id="137" Count="2" />
      <LineId Id="136" Count="0" />
      <LineId Id="142" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="105" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>